use crate::ast::*;

// lalrpop 里的约定
grammar;

// 约束 lexer 的行为
match {
  // 跳过空白符和注释
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  r"/\*([^*]|\*[^/])*\*/" => {},
  // 剩下的情况采用默认方式处理
  _
}

pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit { <> };

FuncDef: FuncDef = {
  <func_type: FuncType> <ident: Ident> "(" ")" <block: Block> => {
    FuncDef { <> }
  }
}

FuncType: FuncType = "int" => FuncType::Int;

Block: Block = "{" <block_items: (BlockItem)*> "}" => Block { <> };

BlockItem: BlockItem = {
  <decl: Decl> => BlockItem::Decl(decl),
  <stmt: Stmt> => BlockItem::Stmt(stmt),
}

Decl: Decl = <const_decl: ConstDecl> => Decl { <> };

ConstDecl: ConstDecl = {
  "const" <b_type: BType> <head: ConstDef> <rest: ("," <ConstDef>)*> ";" => {
    let mut defs = vec![head];
    defs.extend(rest);
    ConstDecl { b_type, const_defs: defs }
  }
}

BType: BType = "int" => BType::Int;

ConstDef: ConstDef = <ident: Ident> "=" <const_init_val: ConstInitVal> => ConstDef { ident, const_init_val };

ConstInitVal: ConstInitVal = <const_exp: ConstExp> => ConstInitVal { <> };

ConstExp: ConstExp = <exp: Exp> => ConstExp{ <> };

Stmt: Stmt = "return" <exp: Exp> ";" => Stmt { <> };

Exp: Exp = <lor_exp: LOrExp> => Exp{ <> };

AddExp: AddExp = {
  <mul_exp: MulExp> => AddExp::Mul(<>),
  <add_exp: AddExp> "+" <mul_exp: MulExp> => AddExp::Add { add: Box::new(add_exp), op: AddOp::Plus, mul: mul_exp },
  <add_exp: AddExp> "-" <mul_exp: MulExp> => AddExp::Add { add: Box::new(add_exp), op: AddOp::Minus, mul: mul_exp },
}

MulExp: MulExp = {
  <unary_exp: UnaryExp> => MulExp::Unary(<>),
  <mul_exp: MulExp> "*" <unary_exp: UnaryExp> => MulExp::Mul { mul: Box::new(mul_exp), op: MulOp::Mul, unary: unary_exp },
  <mul_exp: MulExp> "/" <unary_exp: UnaryExp> => MulExp::Mul { mul: Box::new(mul_exp), op: MulOp::Div, unary: unary_exp },
  <mul_exp: MulExp> "%" <unary_exp: UnaryExp> => MulExp::Mul { mul: Box::new(mul_exp), op: MulOp::Mod, unary: unary_exp },
};

UnaryExp: UnaryExp = {
  <primary_exp: PrimaryExp> => UnaryExp::PrimaryExp(Box::new( <> )),
  <unary_op: UnaryOp> <unary_exp: UnaryExp> =>
    UnaryExp::Unary{op: unary_op, exp: Box::new(unary_exp)},
};

LOrExp: LOrExp = {
  <land_exp: LAndExp> => LOrExp::LAnd( <> ),
  <lor_exp: LOrExp> "||" <land_exp: LAndExp> => LOrExp::LOr{ lor: Box::new(lor_exp), land: land_exp },
}

LAndExp: LAndExp = {
  <eq_exp: EqExp> => LAndExp::Eq( <> ),
  <land_exp: LAndExp> "&&" <eq_exp: EqExp> => LAndExp::LAnd{ land: Box::new(land_exp), eq: eq_exp },
}

EqExp: EqExp = {
  <rel_exp: RelExp> => EqExp::Rel( <> ),
  <eq_exp: EqExp> "==" <rel_exp: RelExp> => EqExp::Eq{ eq: Box::new(eq_exp), op: EqOp::Eq, rel: rel_exp },
  <eq_exp: EqExp> "!=" <rel_exp: RelExp> => EqExp::Eq{ eq: Box::new(eq_exp), op: EqOp::Neq, rel: rel_exp },
};

RelExp: RelExp = {
  <add_exp: AddExp> => RelExp::Add( <> ),
  <rel_exp: RelExp> "<" <add_exp: AddExp> => RelExp::Rel{ rel: Box::new(rel_exp), op: RelOp::Lt, add: add_exp },
  <rel_exp: RelExp> ">" <add_exp: AddExp> => RelExp::Rel{ rel: Box::new(rel_exp), op: RelOp::Gt, add: add_exp },
  <rel_exp: RelExp> "<=" <add_exp: AddExp> => RelExp::Rel{ rel: Box::new(rel_exp), op: RelOp::Le, add: add_exp },
  <rel_exp: RelExp> ">=" <add_exp: AddExp> => RelExp::Rel{ rel: Box::new(rel_exp), op: RelOp::Ge, add: add_exp },
};

PrimaryExp: PrimaryExp = {
  "(" <exp: Exp> ")" => PrimaryExp::Exp(Box::new( <> )),
  <lval: LVal> => PrimaryExp::LVal(lval),
  <num: Number> => PrimaryExp::Number(num),
};

LVal: LVal = <ident: Ident> => LVal{ <> };

UnaryOp: UnaryOp = {
  "+" => UnaryOp::Plus,
  "-" => UnaryOp::Minus,
  "!" => UnaryOp::Not,
};

Number: i32 = <num: IntConst> => <>;

// 如果匹配到标识符, 就返回这个字符串
// 一对尖括号在此处指代的是正则表达式匹配到的字符串 (&str)
// 关于尖括号到底代表什么, 请 RTFM
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

// 对整数字面量的处理方式: 把匹配到的字符串按对应进制转换成数字
IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}
